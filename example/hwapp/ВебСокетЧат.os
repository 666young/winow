&Пластилин Перем БрокерСообщенийВебСокетов;

Перем КешИменПользователей;

&Контроллер("/chat")
Процедура ПриСозданииОбъекта()
	КешИменПользователей = Новый Соответствие();
КонецПроцедуры

&ТочкаМаршрута("message")
Процедура ВходящееСообщение(Идентификатор, Топик, Сообщение) Экспорт

	ИмяПользователя = КешИменПользователей.Получить(Идентификатор);

	Массив = Новый Массив();
	Массив.Добавить(Идентификатор);

	ТекстПолучения = ФорматированноеСообщение(ИмяПользователя, Сообщение, Истина);
	ТекстОтправки = ФорматированноеСообщение(ИмяПользователя, Сообщение, Ложь);
	
	БрокерСообщенийВебСокетов.ОтправитьСообщениеВсемКроме(Топик, ТекстПолучения, Массив);
	БрокерСообщенийВебСокетов.ОтправитьСообщениеСписку(Топик, ТекстОтправки, Массив);

КонецПроцедуры

&Отображение("./hwapp/view/chat.html")
&ТочкаМаршрута("")
Процедура Главная(Сессия, Ответ) Экспорт
	Имя = КешИменПользователей.Получить(Сессия.Идентификатор());
	Если Имя = Неопределено Тогда
		Ответ.Перенаправить("/chat/login");
	КонецЕсли;
КонецПроцедуры

&ТочкаМаршрута("login")
&Отображение("./hwapp/view/chatlogin.html")
Процедура Логин() Экспорт
КонецПроцедуры

&ТочкаМаршрута("loginprocess")
Процедура ОбработкаЛогина(ИмяПользователя, Сессия, Ответ) Экспорт
	КешИменПользователей.Вставить(Сессия.Идентификатор(), ИмяПользователя);
	Ответ.Перенаправить("/chat");
КонецПроцедуры

&ПриПодключенииВебСокета("/chat/message")
Процедура ПриПодключенииПользователя(Идентификатор) Экспорт
	ИмяПользователя = КешИменПользователей.Получить(Идентификатор);

	ТекстСообщения = ФорматированноеСообщение("Оракул", ИмяПользователя + " вошел в чат", Истина);

	БрокерСообщенийВебСокетов.ОтправитьСообщениеВсем("/chat/message", ТекстСообщения);
КонецПроцедуры

&ПриОтключенииВебСокета("/chat/message")
Процедура ПриОтключенииПользователя(Идентификатор) Экспорт
	ИмяПользователя = КешИменПользователей.Получить(Идентификатор);

	ТекстСообщения = ФорматированноеСообщение("Оракул", ИмяПользователя + " покинул чат", Истина);

	БрокерСообщенийВебСокетов.ОтправитьСообщениеВсем("/chat/message", ТекстСообщения);
КонецПроцедуры

Функция ФорматированноеСообщение(Автор, Текст, Получен)
	ОбъектДляПарсинга = Новый Структура("Author, Time, Text, rcv", Автор, Строка(ТекущаяДата()), Текст, Получен);

	Запись = новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, ОбъектДляПарсинга);

	Возврат Запись.Закрыть();
КонецФункции