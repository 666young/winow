&Пластилин Перем БрокерСообщенийВебСокетов;

Перем КешИменПользователей;

&Контроллер("/chat")
Процедура ПриСозданииОбъекта()
	КешИменПользователей = Новый Соответствие();
КонецПроцедуры

&ТочкаМаршрута("message")
Процедура ВходящееСообщение(Идентификатор, Топик, Сообщение) Экспорт

	ИмяПользователя = КешИменПользователей.Получить(Идентификатор);

	Сообщить(СтрШаблон("Получено сообщение %1 от %2", Сообщение, ИмяПользователя));


	Массив = Новый Массив();
	Массив.Добавить(Идентификатор);

	ТекстПолучения = ФорматированноеСообщение(ИмяПользователя, Сообщение, Истина);
	ТекстОтправки = ФорматированноеСообщение(ИмяПользователя, Сообщение, Ложь);
	
	БрокерСообщенийВебСокетов.ОтправитьСообщениеВсемКроме(Топик, ТекстПолучения, Массив);
	БрокерСообщенийВебСокетов.ОтправитьСообщениеСписку(Топик, ТекстОтправки, Массив);

КонецПроцедуры

&Отображение("./hwapp/view/chat.html")
&ТочкаМаршрута("")
Процедура Главная(Сессия, Ответ) Экспорт
	Имя = КешИменПользователей.Получить(Сессия.Идентификатор());
	Если Имя = Неопределено Тогда
		Ответ.Перенаправить("/chat/login");
	КонецЕсли;
КонецПроцедуры

&ТочкаМаршрута("login")
&Отображение("./hwapp/view/chatlogin.html")
Процедура Логин() Экспорт
КонецПроцедуры

&ТочкаМаршрута("loginprocess")
Процедура ОбработкаЛогина(ИмяПользователя, Сессия, Ответ) Экспорт
	Если НЕ ЗначениеЗаполнено(ИмяПользователя) Тогда
		ГенераторСлучайныхЧисел = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
		ИмяПользователя = "Noname" + Строка(ГенераторСлучайныхЧисел.СлучайноеЧисло(1, 999));
	КонецЕсли;
	Сообщить(СтрШаблон("Регистрация пользователя %1", ИмяПользователя));
	КешИменПользователей.Вставить(Сессия.Идентификатор(), ИмяПользователя);
	Ответ.Перенаправить("/chat");
КонецПроцедуры

&ПриПодключенииВебСокета("/chat/message")
Процедура ПриПодключенииПользователя(Идентификатор) Экспорт
	
	ИмяПользователя = КешИменПользователей.Получить(Идентификатор);

	Сообщить(СтрШаблон("Подключился %1", ИмяПользователя));

	ТекстСообщенияГостю = ФорматированноеСообщение("Оракул", "Привет " + ИмяПользователя + " !", Истина);
	
	БрокерСообщенийВебСокетов.ОтправитьСообщениеВсем("/chat/message", ТекстСообщенияГостю);
КонецПроцедуры

&ПриОтключенииВебСокета("/chat/message")
Процедура ПриОтключенииПользователя(Идентификатор) Экспорт
	ИмяПользователя = КешИменПользователей.Получить(Идентификатор);

	Сообщить(СтрШаблон("Отключился %1", ИмяПользователя));

	ТекстСообщения = ФорматированноеСообщение("Оракул", ИмяПользователя + " покинул чат", Истина);

	БрокерСообщенийВебСокетов.ОтправитьСообщениеВсем("/chat/message", ТекстСообщения);
КонецПроцедуры

Функция ФорматированноеСообщение(Автор, Текст, Получен)
	ОбъектДляПарсинга = Новый Структура("Author, Time, Text, rcv", Автор, Формат(ТекущаяДата(), "ДФ=ЧЧ:мм"), Текст, Получен);

	Запись = новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, ОбъектДляПарсинга);

	Возврат Запись.Закрыть();
КонецФункции