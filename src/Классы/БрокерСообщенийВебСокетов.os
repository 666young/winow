&Пластилин Перем ШифрованиеВебСокета;
&Пластилин Перем Маршрутизатор;
&Пластилин Перем КонструкторыПараметров;
&Пластилин Перем СоединенияВебСокетов;

Перем Рефлектор;
Перем КешОбработчиков;

&Желудь
Процедура ПриСозданииОбъекта()
	Рефлектор = Новый Рефлектор();	
	КешОбработчиков = Новый Соответствие();
КонецПроцедуры

Процедура ВходящееСообщение(Идентификатор, Топик, ДвоичныеДанныеСообщения) Экспорт

	Сообщение = ШифрованиеВебСокета.РаспаковатьСообщение(ДвоичныеДанныеСообщения);

	Обработчик = НайтиОбработчик(Топик);

	Параметры = КонструкторыПараметров.СформироватьМассивНеобходимыхПараметров(Обработчик.Действие.Параметры, Новый Структура("Идентификатор, Топик, Сообщение", Идентификатор, Топик, Сообщение));
	
	Рефлектор.ВызватьМетод(Обработчик.Действие.Желудь, Обработчик.Действие.ИмяМетода, Параметры);

КонецПроцедуры

Процедура ОтправитьСообщение(Топик, Сообщение, Идентификатор) Экспорт
	ДанныеСоединения = СоединенияВебСокетов.ПолучитьСоединениеПоИдентификатору(Топик, Идентификатор);
	Если НЕ ДанныеСоединения = Неопределено Тогда
		УпакованноеСообщение = ШифрованиеВебСокета.ЗапаковатьСообщениеТекстовоеСообщение(Сообщение);
		ДанныеСоединения.Соединение.ОтправитьДвоичныеДанные(УпакованноеСообщение);
	КонецЕсли;
КонецПроцедуры

Процедура ОтправитьСообщениеВсем(Топик, Сообщение) Экспорт
	Соединения = СоединенияВебСокетов.ПолучитьСоединенияПоТопику(Топик);
	УпакованноеСообщение = ШифрованиеВебСокета.ЗапаковатьСообщениеТекстовоеСообщение(Сообщение);
	Для Каждого ДанныеСоединения из Соединения Цикл
		ДанныеСоединения.Соединение.ОтправитьДвоичныеДанные(УпакованноеСообщение);
	КонецЦикла;
КонецПроцедуры

Процедура ОтправитьСообщениеСписку(Топик, Сообщение, СписокИдентификаторов) Экспорт
	Соединения = СоединенияВебСокетов.ПолучитьСоединенияПоТопикуСпискуИдентификаторов(Топик, СписокИдентификаторов);
	УпакованноеСообщение = ШифрованиеВебСокета.ЗапаковатьСообщениеТекстовоеСообщение(Сообщение);
	Для Каждого ДанныеСоединения из Соединения Цикл
		ДанныеСоединения.Соединение.ОтправитьДвоичныеДанные(УпакованноеСообщение);
	КонецЦикла;
КонецПроцедуры

Процедура ОтправитьСообщениеВсемКроме(Топик, Сообщение, СписокИсключенийИдентификаторов) Экспорт
	Соединения = СоединенияВебСокетов.ПолучитьСоединенияПоТопикуКромеСпискаИдентификаторов(Топик, СписокИсключенийИдентификаторов);
	УпакованноеСообщение = ШифрованиеВебСокета.ЗапаковатьСообщениеТекстовоеСообщение(Сообщение);
	Для Каждого ДанныеСоединения из Соединения Цикл
		ДанныеСоединения.Соединение.ОтправитьДвоичныеДанные(УпакованноеСообщение);
	КонецЦикла;
КонецПроцедуры

Процедура ПриПодключении(Топик, Идентификатор) Экспорт
	
КонецПроцедуры

Процедура ПриОтключении(Топик, Идентификатор) Экспорт
	
КонецПроцедуры

Функция НайтиОбработчик(Топик)

	Обработчик = КешОбработчиков.Получить(Топик);
	Если Обработчик = Неопределено Тогда
		Обработчик = Маршрутизатор.НайтиОбработчикИПараметрыПоПолномуПути(Топик);
		КешОбработчиков.Вставить(Топик, Обработчик);
	КонецЕсли;

	Возврат Обработчик;
	
КонецФункции