
&Пластилин Перем Настройки Экспорт;

Перем Перечисления;
Перем ШифрованиеТЛС;

&Желудь
Процедура ПриСозданииОбъекта(
							&Пластилин("Перечисления") _Перечисления,
							&Пластилин("ШифрованиеТЛС") _ШифрованиеТЛС
							)
	Перечисления = _Перечисления;
	ШифрованиеТЛС = _ШифрованиеТЛС;
КонецПроцедуры

Процедура ОтправитьОтвет(Ответ, Соединение, Ключ = Неопределено) Экспорт

	МассивОтвета = Новый Массив();

	МассивОтвета.Добавить(ПолучитьШапкуОтвета(Ответ));

	ДобавитьВМассивСоответствиеКакСтроки(МассивОтвета, Ответ.Заголовки);

	Для Каждого Кука из Ответ.Куки.ПолучитьМассивСтрокКук() Цикл
		МассивОтвета.Добавить(Кука);
	КонецЦикла;

	МассивОтвета.Добавить(Перечисления.Разделитель);

	ТекстСтатусИЗаголовки = СтрСоединить(МассивОтвета, Перечисления.Разделитель);
	
	ДвоичныеДанныеТекстСтатусИЗаголовки = ПолучитьДвоичныеДанныеИзСтроки(ТекстСтатусИЗаголовки, "utf-8");

	МассивДвоичныхДанных = Новый Массив();

	МассивДвоичныхДанных.Добавить(ДвоичныеДанныеТекстСтатусИЗаголовки);

	Если ЗначениеЗаполнено(Ответ.ТелоДвоичныеДанные) Тогда
		МассивДвоичныхДанных.Добавить(Ответ.ТелоДвоичныеДанные);

	ИначеЕсли ЗначениеЗаполнено(Ответ.ТелоТекст) Тогда
		МассивДвоичныхДанных.Добавить(ПолучитьДвоичныеДанныеИзСтроки(Ответ.ТелоТекст, "utf-8"));	
		
	КонецЕсли;

	ДвоичныеДанныеОтвета = СоединитьДвоичныеДанные(МассивДвоичныхДанных);

	Если Настройки.ИспользоватьШифрование = Истина Тогда
		ДвоичныеДанныеОтвета = ШифрованиеТЛС.Зашифровать(ДвоичныеДанныеОтвета, Ключ);
	КонецЕсли;

	ОтправитьВПопытке(Соединение, ДвоичныеДанныеОтвета);
	
КонецПроцедуры

Процедура ОтправитьВПопытке(Соединение, ДвоичныеДанныеОтвета)
	Попытка
		Соединение.ОтправитьДвоичныеДанные(ДвоичныеДанныеОтвета);	
	Исключение
		// TODO: ДОБАВИТЬ ЛОГГЕР
		Сообщить("Не удалось отправить ответ");	
	КонецПопытки;
КонецПроцедуры

Процедура ДобавитьВМассивСоответствиеКакСтроки(Массив, Соответствие)
	Для Каждого КиЗ Из Соответствие Цикл
		Массив.Добавить(СтрШаблон("%1: %2", КиЗ.Ключ, КиЗ.Значение));	
	КонецЦикла
КонецПроцедуры

Функция ПолучитьШапкуОтвета(Ответ)
	Возврат СтрШаблон("HTTP/1.1 %1 %2", Ответ.СостояниеКод, Ответ.СостояниеТекст);	
КонецФункции
