Перем КонтекстПриложения;

&Желудь
Процедура ПриСозданииОбъекта(
							&Пластилин("КонтекстПриложения") _КонтекстПриложения
							)
	КонтекстПриложения = _КонтекстПриложения;
КонецПроцедуры

Функция ПолучитьЗапрос(Соединение) Экспорт
	
	Запрос = КонтекстПриложения.ПолучитьЖелудь("ВходящийЗапрос");

	ДанныеЗапроса = Соединение.ПрочитатьДвоичныеДанные();;

	ТекстЗапроса = ПолучитьСтрокуИзДвоичныхДанных(ДанныеЗапроса, "utf-8");

	СтруктураЗапроса = РазобратьТексЗапроса(ТекстЗапроса);

	Запрос.ТекстЗапроса = ТекстЗапроса;
	Запрос.Соединение = Соединение;
	Запрос.ДвоичныеДанные = ДанныеЗапроса;

	ЗаполнитьЗначенияСвойств(Запрос, СтруктураЗапроса);

	Возврат Запрос;

КонецФункции

Функция РазобратьТексЗапроса(ТекстЗапроса)
	Результат = Новый Структура();

	МассивСтрок = СтрРазделить(ТекстЗапроса, Символы.ПС, Ложь);

	Если МассивСтрок.Количество() > 0 Тогда
		ПерваяСтрока = СтрРазделить(МассивСтрок[0], " ");
		Результат.Вставить("ТипЗапроса", ПерваяСтрока[0]);
		Результат.Вставить("ПолныйПуть", РаскодироватьСтроку(ПерваяСтрока[1],СпособКодированияСтроки.КодировкаURL));
		
		ПутьИПараметры = РазделитьСтроку(Результат.ПолныйПуть, "?");

		Результат.Вставить("ПараметрыИменные", ПараметрыИзТекста(ПутьИПараметры.Право));
		Результат.Вставить("Путь", ПутьИПараметры.Лево);
		
		Заголовки = Новый Соответствие();
		Результат.Вставить("Заголовки", Заголовки);

		Для Сч = 1 По МассивСтрок.ВГраница() Цикл
			ДанныеСтроки = РаспарситьСтрокуЗаголовка(МассивСтрок[Сч]);
			Заголовки.Вставить(ДанныеСтроки.Имя, ДанныеСтроки.Значение);
		КонецЦикла;

	КонецЕсли;

	Возврат Результат;
КонецФункции

Функция ПараметрыИзТекста(ПараметрыТекстом)

	Результат = Новый Соответствие();
	
	Если ЗначениеЗаполнено(ПараметрыТекстом) Тогда
		МассивПараметров = СтрРазделить(ПараметрыТекстом, "&", Ложь);
		Для Каждого ТекПараметрСтрокой из МассивПараметров Цикл
			ТекПараметр = РазделитьСтроку(ТекПараметрСтрокой, "=");
			Результат.Вставить(ТекПараметр.Лево, ТекПараметр.Право);
		КонецЦикла;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция РазделитьСтроку(Строка, Разделитель)
	Результат = Новый Структура("Лево, Право","","");
	ПозицияРазделителя = СтрНайти(Строка, Разделитель);

	Если ПозицияРазделителя = 0 Тогда
		Результат.Лево = Строка;
	Иначе
		Результат.Лево = Лев(Строка, ПозицияРазделителя - 1);
		Результат.Право = Сред(Строка, ПозицияРазделителя + 1);
	КонецЕсли;

	Возврат Результат;
КонецФункции

Функция РаспарситьСтрокуЗаголовка(СтрокаЗаголовка)
	РазделеннаяСтрока = РазделитьСтроку(СтрокаЗаголовка, ":");
	Возврат Новый Структура("Имя, Значение", РазделеннаяСтрока.Лево, РазделеннаяСтрока.Право);
КонецФункции