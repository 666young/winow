Перем ОбщийКонтейнер;
Перем СоответствиеПутей;

&Желудь
Процедура ПриСозданииОбъекта(&Пластилин("ОбщийКонтейнер") _ОбщийКонтейнер)
	ОбщийКонтейнер = _ОбщийКонтейнер;
	СоответствиеПутей = Новый Соответствие();
КонецПроцедуры

Процедура ДобавитьКаталогСтатичныхФайлов(КаталогФайлов, ПутьНаСайте = "/") Экспорт
	Для Каждого ТипФайла из ОбщийКонтейнер.Перечисления.ОписанияТиповРасширенийФайлов Цикл 
		Файлы = НайтиФайлы(КаталогФайлов, "*." + ТипФайла.Ключ, Истина);
		Для каждого Файл Из Файлы Цикл
			ДобавитьМаршрутДоФайла(Файл, КаталогФайлов, ПутьНаСайте, ТипФайла.Значение);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ОтдатьФайл(ВходящийЗапрос, Ответ) Экспорт
	
	ОписаниеФайла = СоответствиеПутей[ВходящийЗапрос.Путь];

	Если ОписаниеФайла = Неопределено Тогда
		Ответ = ОбщийКонтейнер.ФабрикаОтветов.НовыйОтвет404();
	Иначе
		Ответ.Заголовки["Content-Type"] = ОписаниеФайла.ТипКонтента;
		Ответ.ТелоДвоичныеДанные = Новый ДвоичныеДанные(ОписаниеФайла.ПутьНаДиске);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьМаршрутДоФайла(Файл, знач КаталогФайлов, знач ПутьНаСайте, знач ТипКонтента)
	
	Если Не СтрЗаканчиваетсяНа(КаталогФайлов, "/") Тогда
		КаталогФайлов = КаталогФайлов + "/";
	КонецЕсли;

	ПутьВПодкаталоге = ПутьВПодкаталоге(Файл.ПолноеИмя, КаталогФайлов);

	РасположениеНаСайте = ОбъединитьПути(ПутьНаСайте, ПутьВПодкаталоге);

	СоответствиеПутей.Вставить(РасположениеНаСайте, Новый Структура("ПутьНаДиске, ТипКонтента", Файл.ПолноеИмя, ТипКонтента));

	ОбщийКонтейнер.Маршрутизатор.ДобавитьМаршрут(РасположениеНаСайте, Новый Действие(ЭтотОбъект, "ОтдатьФайл"));

КонецПроцедуры

Функция ПутьВПодкаталоге(знач ПолныйПуть, знач КаталогФайлов)

	Если СтрНачинаетсяС(КаталогФайлов, ".") Тогда
		КаталогФайлов = Сред(КаталогФайлов, 2);
	КонецЕсли;

	Вхождение = СтрНайти(ПолныйПуть, КаталогФайлов, НаправлениеПоиска.СКонца);

	Результат = Сред(ПолныйПуть, Вхождение + СтрДлина(КаталогФайлов));

	Возврат Результат;
	
КонецФункции