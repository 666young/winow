Перем КонтекстПриложения;
Перем ФабрикаШаблонов;

&Желудь
Процедура ПриСозданииОбъекта(
							&Пластилин("КонтекстПриложения") _КонтекстПриложения,
							&Пластилин("ФабрикаШаблонов") _ФабрикаШаблонов
							)
	КонтекстПриложения = _КонтекстПриложения;
	ФабрикаШаблонов = _ФабрикаШаблонов;
КонецПроцедуры

Функция НовыйОтвет() Экспорт
	НовыйОтвет = КонтекстПриложения.ПолучитьЖелудь("Ответ");
	ЗаполнитьОтветПредопределеннымиЗначениям(НовыйОтвет);
	Возврат НовыйОтвет;
КонецФункции

Функция НовыйОтвет404() Экспорт
	НовыйОтвет = НовыйОтвет();
	НовыйОтвет.УстановитьСостояниеНеНайдено();
	НовыйОтвет.Шаблон = ФабрикаШаблонов.ПолучитьШаблонИзФайла(404);
	Возврат НовыйОтвет;
КонецФункции

Функция НовыйОтвет500(ТекстОшибки = "") Экспорт
	НовыйОтвет = НовыйОтвет();
	НовыйОтвет.УстановитьСостояниеОшибка();
	НовыйОтвет.ТелоТекст = СтрШаблон(ДоктайпХТМЛИДив(),"500");
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		НовыйОтвет.ТелоТекст = НовыйОтвет.ТелоТекст + СтрШаблон("<div> %1 </div>", ТекстОшибки);	
	КонецЕсли;
	Возврат НовыйОтвет;
КонецФункции

Функция НовыйОтвет401() Экспорт
	НовыйОтвет = НовыйОтвет();
	НовыйОтвет.УстановитьСостояниеНеАвторизован();
	НовыйОтвет.ТелоТекст = СтрШаблон(ДоктайпХТМЛИДив(),"401");
	Возврат НовыйОтвет;
КонецФункции

Функция НовыйОтвет403() Экспорт
	НовыйОтвет = НовыйОтвет();
	НовыйОтвет.УстановитьСостояниеДоступЗакрыт();
	НовыйОтвет.ТелоТекст = СтрШаблон(ДоктайпХТМЛИДив(),"403");
	Возврат НовыйОтвет;
КонецФункции

Процедура ЗаполнитьОтветПредопределеннымиЗначениям(Ответ)
	Ответ.УстановитьСостояниеОК();
	Ответ.Заголовки["Server"] = "WINOW";
	Ответ.УстановитьТипКонтента("html");
КонецПроцедуры

Функция ДоктайпХТМЛИДив()
	Возврат "<!DOCTYPE html>
			|<div><h1> %1 </h1></div>";
КонецФункции