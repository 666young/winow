
&Желудь
Процедура ПриСозданииОбъекта()

КонецПроцедуры

Функция МагическаяСтрока() Экспорт
	Возврат "258EAFA5-E914-47DA-95CA-C5AB0DC85B11";
КонецФункции

Функция ХешОтветаРукопожатия(КлючРукопожатия) Экспорт
	Результат = СокрЛП(КлючРукопожатия) + МагическаяСтрока();	
	Провайдер = Новый ХешированиеДанных(ХешФункция.SHA1);
	Провайдер.Добавить(Результат);
	Результат = Base64Строка(Провайдер.ХешСумма);
	Возврат Результат;	
КонецФункции

Функция РаспаковатьСообщение(ДвоичныеДанныеСообщения) Экспорт

	Массив = РазделитьДвоичныеДанные(ДвоичныеДанныеСообщения, 1);
	
	ЗначениеРазмера = ДвоичныеДанныеВЧисло(Массив[1]);
	Размер = ПобитовоеИ(ЗначениеРазмера, 127);

	Если Размер = 126 Тогда 
		ИндексНачалаМаски = 4
	ИначеЕсли Размер = 127 Тогда
		ИндексНачалаМаски = 10;
	Иначе
		ИндексНачалаМаски = 2;
	КонецЕсли;

	ИндексНачалаДанных = ИндексНачалаМаски + 4;

	Маска = МассивБайтВМассивЧисел(СрезМассива(Массив, ИндексНачалаМаски, ИндексНачалаДанных - 1));

	Результат = Новый Массив();

	СчетчикМаски = 0;

	Для _Сч = ИндексНачалаДанных По Массив.ВГраница() Цикл
		Код = ПобитовоеИсключительноеИли(ДвоичныеДанныеВЧисло(Массив[_Сч]), Маска[СчетчикМаски % 4]);
		Если НЕ Код = 0 Тогда
			Результат.Добавить(Символ(Код));
		КонецЕсли;
		СчетчикМаски = СчетчикМаски + 1;
	КонецЦикла;

	Возврат СтрСоединить(Результат, "");
КонецФункции

Функция СрезМассива(Массив, Знач Начало, Знач Конец)
	Результат = Новый Массив();
	Для Индекс = Начало По Конец Цикл
		Результат.Добавить(Массив[Индекс]);
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ДвоичныеДанныеВЧисло(ДвоичныеДанные)
	Возврат ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ДвоичныеДанные).Получить(0);
КонецФункции

Функция МассивБайтВМассивЧисел(Массив)
	Результат = Новый Массив();
	Для Каждого Элемент Из Массив Цикл
		Результат.Добавить(ДвоичныеДанныеВЧисло(Элемент));
	КонецЦикла;
	Возврат Результат;
КонецФункции